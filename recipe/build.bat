@echo off

if not defined CONDA_BUILD_CROSS_COMPILATION (
  set CONDA_BUILD_CROSS_COMPILATION=0
)

:: Build and install SBCL (builds in _conda-build dir and installs in PREFIX)
mkdir %SRC_DIR%\_conda-build
if %ERRORLEVEL% neq 0 exit /b %ERRORLEVEL%
cd %SRC_DIR%\_conda-build
  :: robocopy tolerates long paths (xcopy does not) and exit codes 0-7 are success
  robocopy %SRC_DIR%\sbcl-source . /E /NFL /NDL /NJH /NJS
  if %ERRORLEVEL% GEQ 8 exit /b %ERRORLEVEL%

  set "CC=x86_64-w64-mingw32-gcc"

  :: The dll target needs to be added to the GNUmakefile
  :: This cannot be done by patching the source due to the tabulation needed by Makefile syntax
  powershell -noprofile -nologo -command "Add-Content -Path src\runtime\GNUmakefile -Value \"libsbcl.dll: `$(PIC_OBJS)\""
  powershell -noprofile -nologo -command "Add-Content -Path src\runtime\GNUmakefile -Value \"`t`$(CC) -shared -o `$@ `$^ `$(LIBS) `$(SOFLAGS) -Wl,--export-all-symbols -Wl,--out-implib,libsbcl.lib\""

  if %target_platform%==win-arm64 (
    bash make.sh --fancy > nul
  ) else (
    bash make.sh --fancy > nul
  )
  if %ERRORLEVEL% neq 0 exit /b %ERRORLEVEL%

  set "INSTALL_ROOT=%PREFIX%"
  set "SBCL_HOME=%PREFIX%\lib\sbcl"
  bash install.sh > nul
  if %ERRORLEVEL% neq 0 exit /b %ERRORLEVEL%

  :: Install dynamic library. The dll target needs to be added to the GNUmakefile
  bash make-shared-library.sh > nul
  if %ERRORLEVEL% neq 0 exit /b %ERRORLEVEL%

  copy src\runtime\libsbcl.dll %PREFIX%\bin\libsbcl.dll > nul
  copy src\runtime\libsbcl.lib %PREFIX%\lib\libsbcl.lib > nul

cd %SRC_DIR%

:: Copy the license files for conda-recipe compliance
copy %SRC_DIR%\sbcl-source\COPYING %SRC_DIR%\COPYING > nul
copy %SRC_DIR%\sbcl-source\CREDITS %SRC_DIR%\CREDITS > nul

:: Setting conda host environment variables
if not exist "%PREFIX%\etc\conda\activate.d\" mkdir "%PREFIX%\etc\conda\activate.d\"
if not exist "%PREFIX%\etc\conda\deactivate.d\" mkdir "%PREFIX%\etc\conda\deactivate.d\"

copy "%RECIPE_DIR%\scripts\activate.bat" "%PREFIX%\etc\conda\activate.d\sbcl-activate.bat" > nul
if %ERRORLEVEL% neq 0 exit /b %ERRORLEVEL%
copy "%RECIPE_DIR%\scripts\deactivate.bat" "%PREFIX%\etc\conda\deactivate.d\sbcl-deactivate.bat" > nul
if %ERRORLEVEL% neq 0 exit /b %ERRORLEVEL%

:: Provide sbclrc that loads files from sbclrc.d directory
if not exist "%PREFIX%\lib\sbcl\sbclrc.d\" mkdir "%PREFIX%\lib\sbcl\sbclrc.d\"
if %ERRORLEVEL% neq 0 exit /b %ERRORLEVEL%

copy "%RECIPE_DIR%\sbclrc" "%PREFIX%\lib\sbcl\sbclrc" > nul
if %ERRORLEVEL% neq 0 exit /b %ERRORLEVEL%
copy "%RECIPE_DIR%\sbclrc.d\00-README.lisp" "%PREFIX%\lib\sbcl\sbclrc.d\" > nul
if %ERRORLEVEL% neq 0 exit /b %ERRORLEVEL%
